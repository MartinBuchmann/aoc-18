;; -*- ispell-local-dictionary: "en" -*-
;; * Day 12
;; The package
(in-package :aoc-18)
(annot:enable-annot-syntax)

;; I knew that a simple cellular automaton would yield most likely to
;; the solution. After studying Phil! Gold's solution I copied his
;; great idea using bit-vectors for that tasks.

;; * Some basic parameters

;; The initial state string starts with an offset for the prefix
;; "initial state: "
(defparameter *state-prefix-length* 15)

;; The encoding of plants and empty pots
(defparameter *plant* #\#)
(defparameter *empty* #\.)

;; * A structure for the state
;;
;; Because the plants can and will spread across the left and right
;; border of the initial state I will keep an offset for the extra
;; space.
(defstruct state
  (offset 0)
  pots)

;; * Handling the input

;; ** Convert the state-string to a bit-vector
(defun parse-state-string (state)
  "Parses the STATE string and returns a bit vector where each cell
with a plant is represented by 1."
  (iter (with bit-vector = (make-array (length state)
                                       :element-type 'bit
                                       :initial-element 0))
        (for c in-string state with-index i)
        (when (char= c *plant*)
          (setf (sbit bit-vector i) 1))
        (finally (return bit-vector))))

;; ** Reading the initial state                               
(defun parse-initial-state (initial-state-string)
  "Return a STATE structure for the INITIAL-STATE-STRING."
  (let* ((bit-string (subseq initial-state-string *state-prefix-length*))
         (first-plant (position *plant* bit-string))
         (last-plant (position *plant* bit-string :from-end t)))
    (make-state
     :offset (min 0 (- first-plant 4))
     :pots (parse-state-string
            (format nil "窿窿窿磲脲扉篝磲ō骈蝮舡痨犷舂洪铋糸犰屐屙孱屙痿忾舡篝蜷铉磲脲扉篝磲ō灬篝痨犷戾铉翳忾舡篝蜷铉旦洪铋糸犰屐屙孱屙痿┅┅┅换蔑铞弪糸铉翳疳趑弪麸犷轭翦珏鲠祯ㄤ彐躅疳趑弪瞽麸轭疳趑弪瞟⒚镱鲥螋翳玳鲥辛栽乓镦翳蝓戾麸犷轭翦珏鲠祯瀹ㄩ弪镳戾铉翳疳趑弪瞟ǐㄡ箬疳趑弪瞽麸轭篚怏羼疳趑弪ū戾铉翳疳趑弪瞟┅暴ㄩㄣ栳蚪筱栳疳趑弪ū戾铉翳疳趑弪瞟┅痨犷舄癌┅换义徜轭翳蝓戾换换阴戾狎汨狎徙翦篝蜷铉麒弪遽汨汨狎徙翦汜翎脲赭换篝狒搴痨犷矧屙痿澡轶戾徜麸厕巢痫篌殁戾蝓戾螽换巢忾鲥泗矧汜翳躞栾熹犰蝓戾螽澡鲠祯镦翳忾玳鲥换翳轭骘蝽狒轱殒痨犷鏖祆珧秣轭翳铄珏铄蜥糸镱矧铒舢ㄤ彐躅疳蝮瀛蝓戾蝓戾篝蜷铉扉篝⒁弭躜铙犷巢忾鲥泗矧骘犰痫篌殁戾蝓戾螽ㄩ翦鏖翳蝈篚祠磲脲狎蜥ㄥ痿旦哄戾礤铘豉疱р轸洪铋糸犰屐屙孱癌ㄦ矧蝓戾篝蜷铉轭蝓戾篝蜷铉扉篝ㄦ矧疳趑弪瞽篝蜷铉秕翥镯瀛篝蜷铉痧泸搴箴扉骄蝓戾篝蜷铉┅麒孱ㄣ栳蚪筱栳秕翥镯瀛篝蜷铉癌痨犷舄箦翩筲轸蝈篚祠疳趑弪瞽麸轭疳趑弪瞽篝蜷铉┅暴ㄦ轭犰禊蝈趱蝾蝈篚祠┅┅换义徜轭翳溽翎换换骘躅翳躞镦溴篝蝓泗轭绛忾钿鲥蝙箝眇戾弼孱翳秕玷轸轶换铒鲥蝙屐彗犷舢ㄤ彐躅蝈徜轭瘐舯é镳糸镱犰ㄩ铕豸ｐ㈤铕豸蟑轭瘐舯伯豇簪┅⒁遽潴翳轭瘐骘料铆备溽卑犷蝈趱蝾轸狍扉篝镦躺侨杂ㄤ弩趄蹉趱蜷铉忾钿ㄩ铋糸犰篝狒弩屙痿蝈篝蝓戾螬ㄩ翦ㄦ矧扉铄轭骈戾轭瘐躞轭＇蝈徜扉铄ㄣ镬戾泗扉铄┅篱珙矧屙痿扉篝轭轸獒飙篝狒弩蝓戾螬┅换郁矧轭翳溽翎ㄤ彐鲠翦篝轭瘐舄蝈徜轭瘐舯Ⅳ弩趔翦篝蟓辈豇簪┅ㄤ彐鲠翦篝篝狒濯疳蝮瀛轭轸獒飙篝狒铘翦篝轭瘐舄┅ㄤ彐鲠翦篝蝓戾螵疳蝮瀛蝓戾铘翦篝轭瘐舄┅ㄤ彐鲠轭瘐舄蝈徜轭瘐舯㈤铕豸蟑轭瘐舯伯豇簪┅ㄤ彐鲠篝狒濯疳蝮瀛轭轸獒飙篝狒铘轭瘐舄┅ㄤ彐鲠蝓戾螵疳蝮瀛蝓戾铘轭瘐舄┅换澡箴蝈徜轭镦翳痨犷趔ㄤ彐躅箴蝈徜痨犷趔镱沐篝狒蝓戾螬⒁弭躜铙铄鳝篝狒徭翦狃痨轭艺膛麸翳玳鲥釉猎女鏖翳箪雉è沲蝌孱舡镦骟弭镦骟弭ㄣ躜蝈铘痫趔痫趔┅篝狒戾舄è骈蝮舡痨犷痫箝糸镱沲蝌孱舡痫趔┅灬篝痨犷痫箝糸镱沲蝌孱舡痫趔烘蝻憝孱舂铄鳝镦骟弭ō沲蝌孱舡镦骟弭ō骈蝮舡痨犷舂┅铄鳝戾铉翳ǐ灬篝痨犷ō沲蝌孱舡镦骟弭铄鳝镦骟弭┅铄鳝痫趔磲脲狎蜥铄鳝戾铉翳哄戾礤铘豉疱р轸洪铋糸犰屐屙孱癌┅ㄩ翦ㄦ矧忾轭鲥泗矧沲蝌孱舡痫趔骝镯骈蝮舡痨犷麸ǐ灬篝痨犷穿鏖翳轭溴椹ㄦ矧疳趑弪骈蝮翳孱祜玑钿祜玳矧ㄡ箬疳趑弪暴忾舂ｂ北北暴麒孱痨躞筲轸蝓戾疳趑弪瞟箦翩筲轸铄鳝痫趔ǐō沲蝌孱舡镦骟弭铄鳝镦骟弭┅暴┅磲脲篝狒猴骀箦铄鳝镦骟弭吼雉铄鳝痫趔┅┅换羽蝈徜轭翳痨犷趔骘珏铄蜥糸镱ㄤ彐躅箴蝈徜痨犷趔ㄩ铋糸犰篝狒蝓戾珏铄蜥糸镱螬⒁弭躜翳篝狒徭翦桥闻伊栽上斡骘艺膛犷翳晌稍闪汰釉猎女ㄩ翦ㄦ矧珏铄蜥糸镱骝镯麸珏铄蜥糸镱螬ㄦ矧铄鳝篝狒骈蝮箴蝈徜痨犷趔镱沐轭轸獒飙篝狒蝓戾螬翳孱箴蝈徜痨犷趔镱沐铄鳝篝狒蝓戾螬ㄦ矧痱弼轱躞篝狒痱弼轱躞铄鳝篝狒濠蝈犰禊扉脲轸弪狒麒孱ㄡ钿痱弼轱躞篝狒ㄥ聃犰篝狒瀛痫趔铄鳝篝狒濠篝狒瀛痫趔痱弼轱躞篝狒濠┅戾狯磲脲篝狒猴骀箦ǐ篝狒瀛镦骟弭铄鳝篝狒濠íō篝狒瀛镦骟弭铄鳝篝狒濠篝狒瀛镦骟弭痱弼轱躞篝狒濠ō珏铄蜥糸镱珏铄蜥糸镱┅吼雉篝狒瀛痫趔铄鳝篝狒濠┅ㄦ轭犰禊蝈趱蝾铄鳝篝狒濠┅换龄溟铉翳痨犷铛礅弪ㄤ彐躅徜洵痨犷舡铛礅弪篝狒濠鏖翳箪雉镦骟弭痫趔篝狒ㄩ翦ㄦ矧轭鲥泗矧痫趔鏖翳轭溴椹麒孱暴篚眄轭ǐ镦骟弭┅┅┅换嗅螋ㄤ彐躅犸悱辈īㄡ滗痨犷舡铛礅弪箴蝈徜痨犷趔篝狒濯蝓戾螵舶┅换嗅螋ㄤ彐躅犸悱辈īㄡ滗痨犷舡铛礅弪箴蝈徜痨犷趔篝狒濯蝓戾螵蛋鞍鞍鞍鞍癌┅ㄤ彐轭瀛翦篝翦篝辈ㄡ篌弪舡羼踽祓В鞍鞍鞍卑鞍卑鞍氨鞍鞍氨鞍卑氨鞍卑鞍鞍箪雉鲠祯箴蝈徜痨犷趔镱沐翦篝篝狒濯翦篝蝓戾螵ю雉螬ㄡ篌弪舡羼踽巢ㄡ滗痨犷舡铛礅弪箴蝈徜痨犷趔翦篝篝狒濯翦篝蝓戾螵舶┅ㄡ篌弪舡羼踽狈阜ㄡ镢辈岍┅